kb_wptr = $0300
kb_rptr = $0301
kb_flags = $0302

RELEASE   = %00000001
SHIFT     = %00000010
EXC_KEY   = %00000100

kb_buffer = $0200  ; 256-byte kb buffer 0200-02ff

; IRQ vector points here
keyboard_interrupt:
  lda kb_flags
  and #RELEASE   ; check if we're releasing a key
  beq read_key   ; otherwise, read the key

  ; and EXC_KEY   ; check if it's an extended key

  lda kb_flags
  eor #RELEASE   ; flip the releasing bit
  sta kb_flags
  lda VIA_PORTA  ; read key value that's being released
  cmp #$12       ; left shift
  beq shift_up
  cmp #$59       ; right shift
  beq shift_up
keyboard_interrupt_exit:
  RTS
  

shift_up:
  lda kb_flags
  eor #SHIFT  ; flip the shift bit
  sta kb_flags
  jmp keyboard_interrupt_exit
  
read_key:
  lda VIA_PORTA
  cmp #$f0        ; if releasing a key
  beq key_release ; set the releasing bit
  cmp #$12        ; left shift
  beq shift_down
  cmp #$59        ; right shift
  beq shift_down   
  cmp #$6B       ;  left arrow
  beq LEFT_ARROW
  cmp #$74       ;  right arrow
  beq RIGHT_ARROW
  cmp #$75       ;  up arrow
  beq UP_ARROW
  cmp #$72       ;  down arrow
  beq DOWN_ARROW

  tax
  lda kb_flags
  and #SHIFT
  bne shifted_key

  lda keymap, x   ; map to character code
  jmp push_key
  JMP keyboard_interrupt_exit

key_release:
  lda kb_flags
  ora #RELEASE
  sta kb_flags
  JMP keyboard_interrupt_exit

shift_down:
  lda kb_flags
  ora #SHIFT
  sta kb_flags
  jmp keyboard_interrupt_exit

UP_ARROW:
  PHX
  LDX #$41                  ; up arrow A
  JSR SEND_ARROW
  PLX
  JMP keyboard_interrupt_exit

DOWN_ARROW:
  PHX
  LDX #$42                 ; down arrow B
  JSR SEND_ARROW
  PLX
  JMP keyboard_interrupt_exit

RIGHT_ARROW:
  PHX
  LDX #$43                ; right arrow C
  JSR SEND_ARROW
  PLX
  JMP keyboard_interrupt_exit

LEFT_ARROW:
  PHX
  LDX #$44               ; left arrow D
  JSR SEND_ARROW
  PLX
  JMP keyboard_interrupt_exit

SEND_ARROW:
  LDA #PORT_OUTPUT
  STA VIA_DDRA
  lda #$1b        ; send ESC
  STA VIA_PORTA
  JSR pulls_vga
  lda #$5B        ; send [
  STA VIA_PORTA
  JSR pulls_vga
  TXA
  STA VIA_PORTA
  JSR pulls_vga
  LDA #PORT_INPUT
  STA VIA_DDRA
  RTS

pulls_vga:
  LDA #VGA_OE
  STA VIA_PORTB
  LDA #0
  STA VIA_PORTB
  RTS

shifted_key:
  lda keymap_shifted, x   ; map to character code

push_key:
  ldx kb_wptr
  sta kb_buffer, x
  inc kb_wptr
  jmp keyboard_interrupt_exit


  


  ; .org $fd00
keymap: ;0123456789ABCDEF
  .byte "????????????? `?" ; 00-0F
  .byte "?????q1???zsaw2?" ; 10-1F
  .byte "?cxde43?? vftr5?" ; 20-2F
  .byte "?nbhgy6???mju78?" ; 30-3F
  .byte "?,kio09??./l;p-?" ; 40-4F
  .byte "??'?[=????",$0a,"]?\??" ; 50-5F
  .byte "?????????1?47???" ; 60-6F
  .byte "0.2568",$1b,"??+3-*9??" ; 70-7F
  .byte "????????????????" ; 80-8F
  .byte "????????????????" ; 90-9F
  .byte "????????????????" ; A0-AF
  .byte "????????????????" ; B0-BF
  .byte "????????????????" ; C0-CF
  .byte "????????????????" ; D0-DF
  .byte "????????????????" ; E0-EF
  .byte "????????????????" ; F0-FF
keymap_shifted:
  .byte "????????????? ~?" ; 00-0F
  .byte "?????Q!???ZSAW@?" ; 10-1F
  .byte "?CXDE#$?? VFTR%?" ; 20-2F
  .byte "?NBHGY^???MJU&*?" ; 30-3F
  .byte "?<KIO)(??>?L:P_?" ; 40-4F
  .byte "??",$22,"?{+?????}?|??" ; 50-5F
  .byte "?????????1?47???" ; 60-6F
  .byte "0.2568???+3-*9??" ; 70-7F
  .byte "????????????????" ; 80-8F
  .byte "????????????????" ; 90-9F
  .byte "????????????????" ; A0-AF
  .byte "????????????????" ; B0-BF
  .byte "????????????????" ; C0-CF
  .byte "????????????????" ; D0-DF
  .byte "????????????????" ; E0-EF
  .byte "????????????????" ; F0-FF
